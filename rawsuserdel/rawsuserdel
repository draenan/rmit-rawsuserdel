#!/usr/bin/env perl -T
# $Id$
$|++;
use strict;
use warnings;
use File::Basename;
use Getopt::Std;
use File::Find;

$Getopt::Std::STANDARD_HELP_VERSION = 1;
our $VERSION = 0.1;

delete @ENV{qw(IFS CDPATH ENV BASH_ENV)};
$ENV{PATH} = "/bin:/usr/bin";

my %options = ();
if (!getopts("c:h:nAUTvVd:u:m:f:", \%options)) {
    exit 1;
}

if ($options{V}) {
    VERSION_MESSAGE();
    exit 0;
}

if ( (!$options{f} and @ARGV == 0) or ($options{T} and $options{U}) or
      ($options{U} and $options{A}) or ($options{T} and $options{A}) ) {
    VERSION_MESSAGE();
    HELP_MESSAGE();
    exit 1;
}

#die "Need to run as root.\n" unless ($< == 0);

my $conf_file;

if ($options{c}) {
    $conf_file = $options{c};
}
else {
    $conf_file = find_conf();
}

die "ERROR: Could not find config file.\n" unless defined $conf_file;

my %config = (
    min_uid   => "500",
    db_host   => "localhost",
    db_name   => "example",
    db_user   => "admin",
    db_passwd => "s3kr1t",
);

parse_config($conf_file, \%config);

if ($config{log_file}) {
    if ($config{log_file} =~ /(^[\w\.-\/]+)$/ ) {
        my $log_file = $1;
        if (-e $log_file) {
            open LOG, ">>", $log_file or die "ERROR: Could not open log file.\n";
        }
        else {
            open LOG, ">", $log_file or die "ERROR: Could not open log file.\n";
        }
    }
    else {
        die "ERROR: Invalid log file specified.\n";
    }
}

if (!$options{T} and defined $options{m}) {
    if ($options{m} =~ /^(\d+)$/ and $options{m} >= 500 ) {
        $config{min_uid} = $1;
    }
    else {
        die "ERROR:  Invalid minimum UID specified.\n";
    }
}
elsif ($options{T} and defined $options{m}) {
    print "The \"-T\" option is set, ignoring minimum UID override.\n" if defined $options{v};
}

if (!$options{U}) {
    if ($options{h}) {
        $config{db_host} = $options{h};
    }

    if ($options{d}) {
        $config{db_name} = $options{d};
    }

    if ($options{u}) {
        $config{db_user} = $options{u};
        print "Enter password for \"$config{db_name}\" user \"$config{db_user}\": ";
        system ("stty -echo");
        $config{db_passwd} = <STDIN>; 
        chomp($config{db_passwd});
        system ("stty echo");
        print "\n";
    }

# TODO Test DB connectivity here.

}
elsif ($options{U} and ($options{h} or $options{d} or $options{u})) {
    print "The \"-U\" option is set, ignoring database config overrides.\n" if defined $options{v};
}

my %passwd = ();
hash_passwd(\%passwd) unless defined $options{T};

my @users = ();
get_users(\@users);

my %victims = ();
get_victims(\%victims, \@users);

my %ac_databases = ();
my %ac_db_creds = ();
if ($options{A}) {
    if (!$options{T} and !$options{U}) {
        foreach my $user (@{$victims{accounts}}) {
            find({ wanted => \&autoconf, untaint => 1 }, "/Users/$user/public_html/");
        }
    }
    elsif ($options{U}) {
        print "The \"-U\" option is set, ignoring \"-A\" option.\n" if defined $options{v};
    }
    else {
        print "The \"-T\" option is set, igonoring \"-A\" option.\n" if defined $options{v};
    }
}

$options{n} = 1;

delete_users(\$victims{accounts}) unless defined $options{T};
delete_tables(\$victims{tables}, \%ac_databases, \%ac_db_creds) unless defined $options{U};

exit 0;

#### Functions.

sub find_conf {
    if (-e "$0".".cf" ) {
        return "$0".".cf";
    }
    else {
        my $cmd_name = basename("$0");
        foreach my $default_file ( ("/usr/local/etc/$cmd_name.cf", "/etc/$cmd_name.cf") ) {
            if (-e $default_file) {
                return $default_file;
            }
        }
   }
}

sub parse_config {
    my ($file, $config) = @_;
    my $line;
    open FILE, "<", $file or die "ERROR: Could not open $file\n";
    while (<FILE>) {
        $line = $_;
        chomp ($line);
        $line =~ s/^\s+//;
        $line =~ s/\s+$//;
        if ($line !~ /^#/ and $line ne "") { 
            $line =~ s/^(\S*)\s+=\s+(\S*)$/$1=$2/;
            $line =~ s/^(.*)=[\'\"](.*)[\'\"]$/$1=$2/;
            my ($key, @value) = split /=/, $line;
            $$config{$key} = join '=', @value;
        }
    }
    close FILE;
}

sub hash_passwd {
    my ($passwd) = @_;
    setpwent();
    while (my ($user, $uid) = (getpwent())[0,2]) {
        $$passwd{$user} = 1  unless $uid < $config{min_uid} or $user =~ /^(?:root|no(?:body|access))/;
    }
    endpwent();
}

sub get_users {
    my ($users) = @_;
    if ($options{f}) {
        open FILE, "<", $options{f} or die "ERROR: Could not open $options{f}\n";
        @$users = <FILE>;
        close FILE;
    }
    else {
        push @$users, @ARGV;
    }
}

sub get_victims {
    my ($victims, $users) = @_;
    foreach my $user (@$users) {
        if ($user =~ /^([A-Za-z_]+[\w]*)$/) {
            $user = $1;
            if ($options{U} or !$options{T}) {
                if (!$passwd{$user}) {
                    print "Will not delete account for user \"$user\": protected, or not present.\n" if defined $options{v};
                }
                else {
                    push @{ $$victims{accounts} }, $user;
                }
            }
            if ($options{T} or !$options{U}) {
                if ($options{T}) {
                    push @{ $$victims{tables} }, $user;
                }
                else {
                    if (!$passwd{$user}) {
                        print "Will not delete tables with prefix \"$user\": doesn't match removable system account.\n" if defined $options{v};
                    }
                    else {
                        push @{ $$victims{tables} }, $user;
                    }
                }
            }
        }
        else {
            print "Ignoring invalid username \"$user\"\n" if defined $options{v};
        }
    }
}

sub autoconf {
    my $file = $File::Find::name;
    return unless -f $file;
    return unless $file =~ /blog(?:\d?)\/wp-config\.php$/;
    print "Checking config in file \"$file\"\n" if defined $options{v};
    open FILE, "<", $file or die "ERROR: Could not open $file\n";
    my ($db_name, $db_user, $db_password, $db_host, $table_prefix);
    while (<FILE>) {
        chomp();
        my $line = $_; 
        if ($line =~ /'DB_NAME'.*'(\w+)'/) {
            $db_name = $1; 
        }   
        elsif ($line =~ /'DB_USER'.*'(\w+)'/) {
            $db_user = $1; 
        }   
        elsif ($line =~ /'DB_PASSWORD'.*'(\w+)'/) {
            $db_password = $1; 
        }   
        elsif ($line =~ /'DB_HOST'.*'([\w\.]+)'/) {
            $db_host = $1; 
        }   
        elsif ($line =~ /\$table_prefix.*'(\w+)'/) {
            $table_prefix = $1; 
        }   
    }   
    close FILE;
    $ac_db_creds{$db_name} = join ':', ($db_user, $db_password) unless defined $ac_db_creds{$db_name};
    push @{ $ac_databases{$db_name} }, (join ':', ($db_host, $table_prefix)) if defined $table_prefix;
}

sub delete_users {
    my ($users) = @_;
    foreach my $user (@{$$users}) {
        my @userdel_cmd = (
            "/usr/sbin/userdel",
            "$user",
        );
        my @zfs_cmd = (
            "/usr/sbin/zfs",
            "destroy",
            "-r",
            "rawshomepool/$user",
        );    
        if (!$options{n}) {
            print "DELETE $user!!\n";
        }
        else {
            if (defined $options{v}) {
                print "TEST: Execute \"@userdel_cmd\"\n";
                print "TEST: Execute \"@zfs_cmd\"\n";
            }
        }
    }

}

sub delete_tables {
    my ($tables, $ac_databases, $ac_db_creds) = @_;
    my $num_db = keys %{$ac_databases};
    if ($num_db > 0) {
        foreach my $database (keys %{$ac_databases}) {
            print "Database: $database  Credentials: $$ac_db_creds{$database}\n";
            print "Prefixes: @{$$ac_databases{$database}}\n";
        }
    }
    else {
        print "Will drop tables with prefixes: @{$$tables}\n";
    }
}

sub HELP_MESSAGE {
    print "Usage: rawsuserdel [-hnvV] -A [-c file] [-h hostname] [-d database]\n";
    print "                   [-u dbuser ] [-m UID] -f file | username...\n\n";
    print "       rawsuserdel [-hnvV] -T [-c file] [-h hostname] [-d database]\n";
    print "                   [-u dbuser] -f file | prefix...\n\n"; 
    print "       rawsuserdel [-hnvV] -U [-c file] [-m UID] -f file | username...\n\n";
    print "   -n             Test mode\n";
    print "   -v             Be verbose\n";
    print "   -V, --version  Prints version message and exits\n";
    print "   -A             Get database details from user config files\n";
    print "   -U             Delete user only, leave database tables alone\n";
    print "   -T             Delete database tables only, leave user alone\n";
    print "   -c             Get configuration from file\n";
    print "   -h             Specify database host (override config file)\n";
    print "   -d             Specify database name (override config file)\n";
    print "   -u             Specify database username (override config file)\n";
    print "   -m             Specify minimum UID (override config file)\n";
    print "   -f             Get usernames or prefixes from file\n";
    print "       --help     Prints this message and exits\n";
    print "   username       Existing RAWS user\n";
    print "   prefix         RAWS database table prefix\n";
}

sub VERSION_MESSAGE {
    print "rawsuserdel version $VERSION\n";
}

# vi: set tabstop=4 shiftwidth=4 expandtab si ai nu:
