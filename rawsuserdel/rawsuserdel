#!/usr/bin/env perl -T
# $Id$

use strict;
use File::Basename;
use Getopt::Std;

$Getopt::Std::STANDARD_HELP_VERSION = 1;
our $VERSION = 0.1;

delete @ENV{qw(IFS CDPATH ENV BASH_ENV)};
$ENV{PATH} = "/bin:/usr/bin";

my %options = ();
getopts("c:hf:ntuvV", \%options);

if ($options{h}) {
    VERSION_MESSAGE();
    HELP_MESSAGE();
    exit 0;
}

if ($options{V}) {
    VERSION_MESSAGE();
    exit 0;
}

if ( (!$options{f} and @ARGV == 0) or ($options{t} and $options{u}) ) {
    VERSION_MESSAGE();
    HELP_MESSAGE();
    exit 1;
}

#die "Need to run as root.\n" unless ($< == 0);

my $conf_file;

if ($options{c}) {
    $conf_file = $options{c};
}
else {
    $conf_file = find_conf();
}

die "ERROR: Could not find config file.\n" unless defined $conf_file;

my %config = (
    min_uid   => "500",
    db_host   => "localhost",
    db_name   => "example",
    db_user   => "admin",
    db_passwd => "s3kr1t",
);

parse_config($conf_file, \%config);

my %passwd = ();
hash_passwd(\%passwd);

my @users = ();
get_users(\@users);

my %victims = ();
get_victims(\%victims, \@users);

foreach my $key (keys %victims) {
    print "$key: @{ $victims{$key} }\n";
}

exit 0;

#### Functions.

sub find_conf {
    if (-e "$0".".cf" ) {
        return "$0".".cf";
    }
    else {
        my $cmd_name = basename("$0");
        foreach my $default_file ( ("/usr/local/etc/$cmd_name.cf", "/etc/$cmd_name.cf") ) {
            if (-e $default_file) {
                return $default_file;
            }
        }
   }
}

sub parse_config {
    my ($file, $config) = @_;
    my $line;
    open FILE, "<", $file or die "ERROR: Could not open $file\n";
    while (<FILE>) {
        $line = $_;
        chomp ($line);
        $line =~ s/^\s+//;
        $line =~ s/\s+$//;
        if ($line !~ /^#/ and $line ne "") { 
            $line =~ s/^(\S*)\s+=\s+(\S*)$/\1=\2/;
            $line =~ s/^(.*)=[\'\"](.*)[\'\"]$/\1=\2/;
            my ($key, @value) = split /=/, $line;
            $$config{$key} = join '=', @value;
        }
    }
    close FILE;
}

sub hash_passwd {
    my ($passwd) = @_;
    setpwent();
    while (my ($user, $uid) = (getpwent())[0,2]) {
        $$passwd{$user} = 1  unless $uid < $config{min_uid} or $user =~ /^(?:root|no(?:body|access))/;
    }
    endpwent();
}

sub get_users {
    my ($users) = @_;
    if ($options{f}) {
        open FILE, "<", $options{f} or die "ERROR: Could not open $options{f}\n";
        @$users = <FILE>;
        close FILE;
    }
    else {
        push @$users, @ARGV;
    }
}

sub get_victims {
    my ($victims, $users) = @_;
    foreach my $user (@$users) {
        if ($user =~ /^([\w^_^-]+)$/) {
            $user = $1;
            if ($options{u} or !$options{t}) {
                if (!$passwd{$user}) {
                    print "Will not delete account for user \"$user\": protected, or not present.\n" if defined $options{v};
                }
                else {
                    push @{ $$victims{accounts} }, $user;
                }
            }
            if (!$options{u}) {
                push @{ $$victims{tables} }, $user;
            }
        }
        else {
            print "Ignoring invalid username \"$user\"\n" if defined $options{v};
        }
    }
}

sub HELP_MESSAGE {
    print "Usage: rawsuserdel [-hnvV] [-t | -u] [-c file] [-f file | username...]\n";
    print "   -h, --help     Prints this message and exits\n";
    print "   -n             Test mode\n";
    print "   -v             Be verbose\n";
    print "   -V, --version  Prints version message and exits\n";
    print "   -t             Delete database tables only, leave user alone\n";
    print "   -u             Delete user only, leave database tables alone\n";
    print "   -c             Get configuration from file\n";
    print "   -f             Get username(s) from file\n";
}

sub VERSION_MESSAGE {
    print "rawsuserdel version $VERSION\n";
}

# vi: set tabstop=4 shiftwidth=4 expandtab si ai nu:
